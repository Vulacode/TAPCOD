10 SHELL "CLS"
20 GOSUB 5380:LOCATE 5,33:PRINT"TAPE CODER"
30 LOCATE 9,29:PRINT"IBM 2FD version 3.01"
40 LOCATE 11,32:PRINT"one-time coder"
50 LOCATE 13,32:PRINT"   May 1988"
60 GOSUB 5480
70 CLEAR:KEY OFF
80 X$="":FOR N=1 TO 10:KEY N,X$:NEXT N
90 DIM ALPH$(100,1),DALPH$(100),SN$(20),RN$(20)
100 :
110 REM **** Main Menu ****
120 M=0:BF=0:ER=-1
130 ON ERROR GOTO 4650
140 WHILE M=0
150   GOSUB 5380:LOCATE 3,31:PRINT"TAPE CODER Ver.3.01"
160   LOCATE 6,36:PRINT"MAIN MENU"
170   LOCATE 8,24:PRINT"F1  : Load messages from disk"
180   LOCATE 9,24:PRINT"F2  : Save messages onto disk"
190   LOCATE 10,24:PRINT"F3  : Delete messages on disk"
200   LOCATE 12,24:PRINT"F4  : Encipher a message in memory"
210   LOCATE 13,24:PRINT"F5  : Decipher a message in memory"
220   LOCATE 15,24:PRINT"F6  : Type a new message"
230   LOCATE 16,24:PRINT"F7  : Edit/read a message in memory"
240   LOCATE 17,24:PRINT"F8  : Print a file"
250   LOCATE 18,24:PRINT"F9  : Save on/Receive from tape"
260   LOCATE 20,24:PRINT"F10 : Quit"
270   CH$=INKEY$:IF CH$="" THEN 270
280   IF LEN(CH$)=2 THEN CH$=RIGHT$(CH$,1)
290   M=ASC(CH$)-58
300 :
310 REM **** Load a file ****
320   WHILE M=1
330     GOSUB 5380:BEEP:LOCATE 2,3:PRINT"LOAD MESSAGES FROM DISK INTO MEMORY"
340     LOCATE 4,3:PRINT"** Ensure 'MESSAGE' disk is in drive B **"
350     LOCATE 21,3:PRINT"Press any key when ready. ESCape to return to Main Menu"
360     CNT$=INKEY$:IF CNT$="" THEN 360
370     IF ASC(CNT$)=27 THEN 110
380     GOSUB 5380:LOCATE 2,3:PRINT"LOAD MESSAGES FROM DISK"
390     ER=0:PRINT:FILES "B:*.*"
400     PRINT:PRINT CHR$(186);" Type in name of message to load"
410     PRINT:PRINT CHR$(186);" or press ENTER to return to Main Menu"
420     PRINT:PRINT CHR$(186);:PRINT " Message name: ":NM$="œ@]":UP=0
430     WHILE NM$<>""
440       V=CSRLIN-UP:LOCATE V-1,15:PRINT SPC(15):LOCATE V-1,15:INPUT": ",NM$
450       IF NM$="" THEN 110
460       ER=1:NAME "B:"+NM$ AS "B:"+NM$
470       SHELL "COPY B:"+NM$+" D:":V=SCREEN(POS(0),CSRLIN)
480       UP=1:VD=CSRLIN-1:LOCATE VD,1:PRINT CHR$(186)
490     WEND
500   WEND
510 :
520 REM **** Save a file ****
530   WHILE M=2
540     GOSUB 5380:BEEP:LOCATE 2,3:PRINT"SAVE MESSAGES IN MEMORY ONTO DISK"
550     LOCATE 4,3:PRINT"** Ensure 'MESSAGE' disk is in drive B **"
560     LOCATE 21,3:PRINT"Press any key when ready. ESCape to return to Main Menu"
570     CNT$=INKEY$:IF CNT$="" THEN 570
580     IF ASC(CNT$)=27 THEN 110
590     GOSUB 5380
600     LOCATE 2,3:PRINT"SAVE MESSAGES IN MEMORY ONTO DISK"
610     ER=2:PRINT:FILES "D:*.*"
620     PRINT:PRINT CHR$(186);" Type in name of message to save"
630     PRINT:PRINT CHR$(186);" or press ENTER to return to Main Menu"
640     PRINT:PRINT CHR$(186);:PRINT " Message name: ":NM$="+=_":UP=0
650     WHILE NM$<>""
660       V=CSRLIN-UP:LOCATE V-1,15:PRINT SPC(15):LOCATE V-1,15:INPUT": ",NM$
670       IF NM$="" THEN 110
680       ER=3:NAME "D:"+NM$ AS "D:"+NM$
690       S$="COPY D:"+NM$+" B:"
700       SHELL S$
710       UP=1:VD=CSRLIN-1:LOCATE VD,1:PRINT CHR$(186)
720     WEND
730   WEND
740 :
750 REM **** Delete a file ****
760   WHILE M=3
770     GOSUB 5380:BEEP:LOCATE 2,3:PRINT"DELETE MESSAGES ON DISK"
780     LOCATE 4,3:PRINT"** Ensure 'MESSAGE' disk is in drive B **"
790     LOCATE 21,3:PRINT"Press any key when ready. ESCape to return to Main Menu"
800     CNT$=INKEY$:IF CNT$="" THEN 800
810     IF ASC(CNT$)=27 THEN 110 ELSE M=3.1
820     WHILE M=3.1
830       GOSUB 5380
840       LOCATE 2,3:PRINT"DELETE MESSAGES ON DISK"
850       ER=4:PRINT:FILES "B:*.*"
860       PRINT:PRINT CHR$(186);" Type in name of message to delete"
870       PRINT:PRINT CHR$(186);" or ENTER to return to Main Menu"
880       PRINT:PRINT CHR$(186);:INPUT " Message name: ",NM$
890       IF NM$="" THEN 110
900       ER=5:NAME "B:"+NM$ AS "B:"+NM$
910       KILL "B:"+NM$
920     WEND
930   WEND
940 :
950 REM **** Encipher ****
960   WHILE M=4
970     GOSUB 5380:BEEP:LOCATE 2,3:PRINT "ENCIPHER A MESSAGE"
980     LOCATE 4,3:PRINT"** Ensure 'SEND' data disk is in drive B **"
990     LOCATE 21,3:PRINT"Press any key when ready. ESCape to return to Main Menu"
1000     CNT$=INKEY$:IF CNT$="" THEN 1000
1010     IF ASC(CNT$)=27 THEN 110 ELSE M=4.1
1020     WHILE M=4.1
1030       GOSUB 5380
1040       LOCATE 2,3:PRINT "ENCIPHER A MESSAGE"
1050       ER=6:PRINT:FILES "D:*"
1060       PRINT:PRINT CHR$(186);" Type in name of message to encipher"
1070       PRINT:PRINT CHR$(186);" or press ENTER to return to Main Menu"
1080       PRINT:PRINT CHR$(186);:INPUT" Message name: ",NM$
1090       IF NM$="" THEN 110
1100       ER=6.1:NAME "D:"+NM$ AS "D:"+NM$
1110       M=4.2
1120       WHILE M=4.2
1130         CLS:GOSUB 5380:LOCATE 6,3:PRINT"Enciphering. Please wait..."
1140         ER=7:OPEN "I",1,"B:SETNUM":INPUT#1,GETNUM:CLOSE 1
1150         IF GETNUM>99 THEN BF=5:GOTO 4400
1160         LOCATE 18,2:PRINT 99-GETNUM;" sets of numbers left"
1170         OPEN "I",1,"B:SDNO":INPUT#1,DINOS$:CLOSE 1
1180         DNO=VAL(MID$(DINOS$,GETNUM*2+1,2))
1190         OPEN "O",1,"C:ALFA.TMP"
1200         OPEN "R",2,"B:SALFJOIN",200
1210         FIELD#2,200 AS FSA$
1220         GET#2,GETNUM+1
1230         SA$=FSA$
1240         PRINT#1,SA$:CLOSE
1250         OPEN "O",1,"C:NUMS.TMP"
1260         OPEN "R",2,"B:SNUMJOIN",130
1270         FIELD#2,130 AS FSN$
1280         FOR N=1 TO 20:GET#2,(GETNUM*20)+N:SN$(N)=FSN$:NEXT N
1290         FOR P=1 TO 20:PRINT#1,SN$(P);:NEXT P
1300         CLOSE
1310         OPEN "I",1,"C:ALFABET"
1320         OPEN "I",2,"C:ALFA.TMP"
1330         OPEN "I",3,"C:NUMS.TMP"
1340         FOR BET = 0 TO 99
1350           ALPH$(BET,0)=INPUT$(1,#1)
1360           VL1$=INPUT$(1,#2)
1370           VL2$=INPUT$(1,#2)
1380           ALPH$(BET,1)=VL1$+VL2$
1390         NEXT BET
1400         CLOSE 1:CLOSE 2
1410         OPEN "O",1,"D:"+NM$+".ENC"
1420         OPEN "I",2,"D:"+NM$
1430         DNO$=ALPH$(DNO,0)
1440         FOR N=1 TO 4:PRINT#1,CHR$(132);:NEXT N:PRINT#1,DNO$;
1450         LOCATE 18,3:PRINT"Character number: "
1460         LENGTH=0:COUNT=0:SLET$="":BM=133
1470         WHILE NOT EOF(2)
1480           AX=ASC(INPUT$(1,#2))
1490           IF AX<32 THEN AX=32
1500           IF AX>131 THEN AX=32
1510           LENGTH=LENGTH+1
1520           LV$=ALPH$(AX-32,1)
1530           RN1$=INPUT$(1,#3):COUNT=COUNT+1
1540           RN2$=INPUT$(1,#3):COUNT=COUNT+1
1550           IF COUNT MOD 2600=0 THEN CLOSE 3:OPEN "I",3,"C:NUMS.TMP":COUNT=0
1560           CD1=VAL(LEFT$(LV$,1))+VAL(RN1$):IF CD1>9 THEN CD1=CD1-10
1570           CD2=VAL(RIGHT$(LV$,1))+VAL(RN2$):IF CD2>9 THEN CD2=CD2-10
1580           CD=CD2*10+CD1:SLET$=""
1590           IF CD<100 THEN SLET$=ALPH$(CD,0)
1600           IF LENGTH MOD 5=0 THEN SLET$=SLET$+CHR$(BM):BM=BM+1
1610           IF BM=138 THEN BM=133
1620           PRINT#1,SLET$;
1630           LOCATE 18,21:PRINT LENGTH;"   "
1640         WEND
1650         FOR N=1 TO 5:PRINT#1,CHR$(138);:NEXT N
1660         PRINT#1,CHR$(13):CLOSE
1670         ER=8:NAME "B:SETNUM" AS "B:SETNUM"
1680         GOSUB 5380:LOCATE 6,3:PRINT"Enciphering finished. Flushing disk..."
1690         LOCATE 18,3:PRINT"Message length: ";LENGTH;" characters"
1700         OPEN "R",1,"B:SALFJOIN",200
1710         FIELD#1,200 AS DSA$
1720         LSET DSA$=STRING$(200,0):PUT#1,GETNUM+1
1730         CLOSE
1740         OPEN "R",1,"B:SNUMJOIN",130
1750         FIELD#1,130 AS DSN$
1760         FOR N=1 TO 20:LSET DSN$=STRING$(130,0):PUT#1,(GETNUM*20)+N:NEXT N
1770         CLOSE
1780         KILL "C:*.TMP"
1790         GETNUM=GETNUM+1
1800         OPEN "O",1,"B:SETNUM":PRINT#1,GETNUM:CLOSE
1810         GOTO 110
1820       WEND
1830     WEND
1840   WEND
1850 :
1860 REM **** Decipher ****
1870   WHILE M=5
1880     GOSUB 5380:BEEP:LOCATE 2,3:PRINT "DECIPHER A MESSAGE"
1890     LOCATE 4,3:PRINT"** Ensure 'RECEIVE' data disk is in drive B **"
1900     LOCATE 21,3:PRINT"Press any key when ready. ESCape to return to Main Menu"
1910     CNT$=INKEY$:IF CNT$="" THEN 1910
1920     IF ASC(CNT$)=27 THEN 110 ELSE M=5.1
1930     WHILE M=5.1
1940       GOSUB 5380
1950       LOCATE 2,3:PRINT "DECIPHER A MESSAGE"
1960       ER=9:PRINT:FILES "D:*"
1970       PRINT:PRINT CHR$(186);" Type in name of message to decipher"
1980       PRINT:PRINT CHR$(186);" or press ENTER to return to Main Menu"
1990       PRINT:PRINT CHR$(186);:INPUT" Message name: ",NM$
2000       IF NM$="" THEN 110
2010       ER=99:NAME "D:"+NM$ AS "D:"+NM$
2020       M=5.2
2030       WHILE M=5.2
2040         GOSUB 5380:LOCATE 6,3:PRINT"Deciphering. Please wait..."
2050         SC=0:N=0
2060         OPEN "I",1,"D:"+NM$
2070         IP$=INPUT$(1,#1)
2080         IF SC=0 AND ASC(IP$)<>132 THEN 2070
2090         IF ASC(IP$)<32 OR ASC(IP$)>132 THEN 2070
2100         IF ASC(IP$)=132 THEN SC=SC+1:GOTO 2070
2110         IF ASC(IP$)>31 AND ASC(IP$)<132 AND SC>=2 THEN DNO=ASC(IP$)-32
2120         CLOSE
2130         ER=10:OPEN "I",1,"B:RDNO":INPUT#1,DX$:CLOSE
2140         WHILE DNO<>N
2150           N=N+1
2160           IF VAL(MID$(DX$,N*2-1,2))=DNO THEN DNO=N
2170         WEND
2180         OPEN "O",1,"C:DALFA.TMP"
2190         OPEN "R",2,"B:RALFJOIN",100
2200         FIELD#2,100 AS FRA$
2210         GET#2,DNO
2220         RA$=FRA$
2230         PRINT#1,RA$:CLOSE
2240         OPEN "O",1,"C:DNUMS.TMP"
2250         OPEN "R",2,"B:RNUMJOIN",130
2260         FIELD#2,130 AS FRN$
2270         FOR N=1 TO 20:GET#2,((DNO-1)*20)+N:RN$(N)=FRN$:NEXT N
2280         FOR P=1 TO 20:PRINT#1,RN$(P);:NEXT P
2290         CLOSE
2300         OPEN "I",1,"D:"+NM$
2310         OPEN "I",2,"C:DALFA.TMP"
2320         OPEN "I",3,"C:DNUMS.TMP"
2330         FOR DBET=0 TO 99
2340           LT$=INPUT$(1,#2)
2350           DALPH$(DBET)=LT$
2360         NEXT DBET
2370         CLOSE 2
2380         IF INSTR(NM$,".enc") OR INSTR(NM$,".ENC") THEN NM$=LEFT$(NM$,LEN(NM$)-4)
2390         OPEN "O",2,"D:"+NM$+".DEC"
2400         GOSUB 5380:LOCATE 2,3:PRINT"Message reads:"
2410         SC=0:CH=0:EC=0:R=4:C=3:CT=0:CK=0:BX=1
2420         DIP$=INPUT$(1,#1)
2430         IF SC=0 AND ASC(DIP$)<>132 THEN 2420
2440         IF ASC(DIP$)=132 THEN SC=SC+1:GOTO 2420
2450         WHILE EC<3
2460           IF BX=6 THEN BX=1
2470           IP$=INPUT$(1,#1)
2480           IF ASC(IP$)<32 AND ASC(IP$)>138 THEN 2470 
2490           IF ASC(IP$)=138 THEN EC=EC+1:GOTO 2770
2500           D=ASC(IP$)-32
2510           IF D>100 AND D<106 THEN BM=D-100:CK=1
2520           WHILE CK
2530             IF CH=5 AND BM=BX THEN BX=BX+1:CH=0:CK=0:GOTO 2460
2540             IF CH=5 AND BM<>BX THEN BX=BX+1:CH=0
2550             IF BX=6 THEN BX=1
2560             IF CH<5 THEN D=99:GOTO 2590
2570           WEND
2580           IF CH=5 THEN CH=0:BX=BX+1
2590           DNR$=STR$(D):IF D<10 THEN DNR$="0"+DNR$
2600           D2$=MID$(DNR$,2,1):D1$=RIGHT$(DNR$,1)
2610           SUBT1$=INPUT$(1,#3):CT=CT+1
2620           IF VAL(D1$)<VAL(SUBT1$) THEN D1$=STR$(VAL(D1$)+10)
2630           DNUM1$=STR$(VAL(D1$)-VAL(SUBT1$))
2640           SUBT2$=INPUT$(1,#3):CT=CT+1
2650           IF CT MOD 2600=0 THEN CLOSE 3:OPEN "I",3,"C:DNUMS.TMP":CT=0
2660           IF VAL(D2$)<VAL(SUBT2$) THEN D2$=STR$(VAL(D2$)+10)
2670           DNUM2$=STR$(VAL(D2$)-VAL(SUBT2$))
2680           DNUM$=DNUM1$+DNUM2$
2690           DNUM=VAL(DNUM$)
2700           PLAIN$=DALPH$(DNUM)
2710           IF CK THEN PLAIN$="?"
2720           LOCATE R,C:PRINT PLAIN$;:PRINT#2,PLAIN$;
2730           C=C+1:CH=CH+1
2740           IF C=79 THEN C=3:R=R+1
2750           IF R=20 THEN R=4
2760           IF CK THEN 2520
2770         WEND
2780         CLOSE
2790         LOCATE 20,1:PRINT CHR$(204):LOCATE 20,2:PRINT BDR$:LOCATE 20,80:PRINT CHR$(185)
2800         LOCATE 21,3:PRINT"This message is in memory. It can be read in the editor, printed and saved"
2810         LOCATE 22,3:PRINT"Press ESCape to redo or any other key to clear screen and flush disk"
2820         CNT$=INKEY$:IF CNT$="" THEN 2820
2830         IF ASC(CNT$)=27 THEN CLOSE:GOTO 70
2840         ER=10.1:NAME "B:RALFJOIN" AS "B:RALFJOIN"
2850         GOSUB 5380:LOCATE 6,3:PRINT"Flushing disk..."
2860         OPEN "R",1,"B:RALFJOIN",100
2870         FIELD#1,100 AS DRA$
2880         LSET DRA$=STRING$(100,0):PUT#1,DNO
2890         CLOSE
2900         OPEN "R",1,"B:RNUMJOIN",130
2910         FIELD#1,130 AS DRN$
2920         FOR N=1 TO 20:LSET DRN$=STRING$(130,0):PUT#1,((DNO-1)*20)+N:NEXT N
2930         CLOSE
2940         KILL "C:*.TMP"
2950         GOTO 110
2960       WEND
2970     WEND
2980   WEND
2990 :
3000 REM **** Type a new message ****
3010   WHILE M=6
3020     GOSUB 5380:BEEP:LOCATE 2,3:PRINT"TYPE A NEW MESSAGE"
3030     LOCATE 4,3:PRINT"Give a name to the message you are about to type"
3040     LOCATE 5,3:PRINT"or press ENTER to return to Main Menu"
3050     LOCATE 7,3:PRINT"The name you give must:"
3060     LOCATE 9,3:PRINT"- only contain alphabet keys"
3070     LOCATE 10,3:PRINT"- not be longer than 8 characters"
3080     LOCATE 11,3:PRINT"- not contain spaces"
3090     LOCATE 12,3:PRINT"- not have an extension"
3100     ER=12:LOCATE 14,3:INPUT "Message name: ",NM$
3110     IF NM$="" THEN 110
3120     NAME "D:"+NM$ AS "D:"+NM$
3130     IF ASC(LEFT$(NM$,1))>47 AND ASC(LEFT$(NM$,1))<58 THEN BF=4:GOTO 4400
3140     IF INSTR(NM$," ") THEN BF=1:GOTO 4400
3150     IF INSTR(NM$,".") THEN BF=2:GOTO 4400
3160     IF LEN(NM$)>8 THEN BF=3:GOTO 4400
3170     M=20
3180   WEND
3190 :
3200 REM **** Edit a message in memory ****
3210   WHILE M=7
3220     GOSUB 5380:BEEP:LOCATE 2,3:PRINT"EDIT/READ A MESSAGE IN MEMORY"
3230     ER=13:PRINT:FILES "D:*.*"
3240     PRINT:PRINT CHR$(186);" Type in name of message to edit or read"
3250     PRINT:PRINT CHR$(186);" or ENTER to return to Main Menu"
3260     PRINT:PRINT CHR$(186);:INPUT" Message name: ",NM$
3270     IF NM$="" THEN 110
3280     NAME "D:"+NM$ AS "D:"+NM$
3290     M=20
3300   WEND
3310 :
3320 REM **** Message editor ****
3330   WHILE M=20
3340     ER=14:SHELL "EDIT D:"+NM$
3350     M=0
3360   WEND
3370 :
3380 REM **** Print file ****
3390   WHILE M=8
3400     GOSUB 5380:WIDTH "LPT1:",75:LOCATE 2,3:PRINT"PRINT A FILE"
3410     ER=15:PRINT:FILES "D:*.*"
3420     PRINT:PRINT CHR$(186);" Type in name of file you wish to print"
3430     PRINT:PRINT CHR$(186);" or press ENTER to return to Main Menu"
3440     PRINT:PRINT CHR$(186);:PRINT" Message name: ":NM$="!?>":UP=0
3450     WHILE NM$<>""
3460       V=CSRLIN-UP:LOCATE V-1,15:PRINT SPC(15):LOCATE V-1,15:INPUT": ",NM$
3470       IF NM$="" THEN 110
3480       ER=16:NAME "D:"+NM$ AS "D:"+NM$
3490       LPRINT CHR$(27);CHR$(64)
3500       P$="TYPE D:"+NM$+" > prn"
3510       SHELL P$
3520       LPRINT CHR$(12)
3530       UP=1:VD=CSRLIN-1:LOCATE VD,1:PRINT CHR$(186)
3540     WEND
3550   WEND
3560 :
3570 REM **** Tape ****
3580   WHILE M=9
3590     GOSUB 5380:LOCATE 5,31:PRINT"TAPE RECORDER MENU"
3600     LOCATE 10,29:PRINT"S   : Save on tape"
3610     LOCATE 12,29:PRINT"R   : Receive from tape"
3620     LOCATE 14,29:PRINT"ESC : Return to Main Menu"
3630     ESAU$=INKEY$:IF ESAU$="" THEN 3630
3640     IF ASC(ESAU$)=27 THEN 110
3650     IF ESAU$<>"R" AND ESAU$="r" AND ESAU$<>"S" AND ESAU$="s" THEN 3630
3660     WHILE ESAU$="s" OR ESAU$="S"
3670       GOSUB 5380:LOCATE 2,3:PRINT"SAVE AN ENCIPHERED MESSAGE ON TAPE"
3680       ER=17:PRINT:FILES "D:*.ENC"
3690       PRINT:PRINT CHR$(186);" Type in name of enciphered message to save on tape"
3700       PRINT:PRINT CHR$(186);" or press ENTER to return to Main Menu"
3710       PRINT:PRINT CHR$(186);:INPUT" Message name: ",NM$
3720       IF NM$="" THEN 110
3730       ER=18:NAME "D:"+NM$ AS "D:"+NM$
3740       GOSUB 5380:BEEP:LOCATE 2,3:PRINT"** Ensure modem is on and connected to recorder properly **"
3750       LOCATE 4,3:PRINT"Press PLAY and RECORD on tape recorder"
3760       LOCATE 21,3:PRINT"Press any key when ready. ESCape to return to Main Menu"
3770       CNT$=INKEY$:IF CNT$="" THEN 3770
3780       IF ASC(CNT$)=27 THEN ESAU$="":GOTO 110
3790       GOSUB 5380:LOCATE 6,3:PRINT"Saving to tape..."
3800       LATER=TIMER+5
3810       WHILE TIMER<LATER
3820       WEND
3830       ER=19:OPEN "COM1:300,N,8,1,CS,DS,CD" AS #1
3840       OPEN "I",2,"D:"+NM$
3850       WHILE NOT EOF (2)
3860         TAPNM$=INPUT$(1,#2):PRINT#1,TAPNM$;
3870       WEND
3880       CLOSE:GOTO 110
3890     WEND
3900     WHILE ESAU$="r" OR ESAU$="R"
3910       GOSUB 5380:LOCATE 2,3:PRINT"RECEIVE AN ENCIPHERED MESSAGE FROM TAPE"
3920       LOCATE 4,3:PRINT"Give a name to the message to be received from tape"
3930       LOCATE 5,3:PRINT"or press ENTER to return to Main Menu"
3940       LOCATE 7,3:PRINT"The name you give must:"
3950       LOCATE 9,3:PRINT"- only contain alphabet keys"
3960       LOCATE 10,3:PRINT"- not be longer than 8 characters"
3970       LOCATE 11,3:PRINT"- not contain spaces"
3980       LOCATE 12,3:PRINT"- not have an extension"
3990       LOCATE 14,3:INPUT"Message name: ",NM$
4000       IF NM$="" THEN 110
4010       IF ASC(LEFT$(NM$,1))>47 AND ASC(LEFT$(NM$,1))<58 THEN BF=4:GOTO 4400
4020       IF INSTR(NM$," ") THEN BF=1:GOTO 4400
4030       IF INSTR(NM$,".") THEN BF=2:GOTO 4400
4040       IF LEN(NM$)>8 THEN BF=3:GOTO 4400
4050       GOSUB 5380:BEEP:LOCATE 2,3:PRINT"** Ensure modem is on and connected to recorder properly **"
4060       LOCATE 21,3:PRINT"Press any key when ready. ESCape to return to Main Menu"
4070       CNT$=INKEY$:IF CNT$="" THEN 4070
4080       IF ASC(CNT$)=27 THEN 110
4090       GOSUB 5370:BEEP:LOCATE 2,3:PRINT"Press PLAY on recorder to transfer '";NM$;"'"
4100       ER=20:EC=0:C=3:R=4
4110       OPEN "COM1:300,N,8,1,CS,DS,CD" AS #1
4120       OPEN "O",2,"D:"+NM$
4130       WHILE EC<=3
4140         BIT$=INPUT$(1,#1)
4150         IF ASC(BIT$)=134 THEN EC=EC+1
4160         LOCATE R,C:PRINT BIT$;:PRINT#2,BIT$;:C=C+1
4170         IF C=79 THEN C=3:R=R+1
4180         IF R=20 THEN R=4
4190       WEND
4200       PRINT#2,CHR$(13):CLOSE
4210       LOCATE 20,1:PRINT CHR$(204):LOCATE 20,2:PRINT BDR$:LOCATE 20,80:PRINT CHR$(185)
4220       LOCATE 21,3:PRINT"This message is in memory. It can now be deciphered"
4230       LOCATE 22,3:PRINT"Press any key to clear screen"
4240       CNT$=INKEY$:IF CNT$="" THEN 4240
4250       GOTO 110
4260     WEND
4270   WEND
4280 :
4290 REM **** Quit ****
4300   WHILE M=10
4310     GOSUB 5380:LOCATE 6,23:BEEP:PRINT"** Are you sure you want to quit? **"
4320     LOCATE 8,20:PRINT"Type Y to quit or N to return to Main Menu"
4330     QUIT$=INKEY$:IF QUIT$="" THEN 4330
4340     IF QUIT$="y" OR QUIT$="Y" THEN SYSTEM
4350     IF QUIT$<>"Y" OR QUIT$<>"y" THEN M=0
4360   WEND
4370 :
4380 WEND
4390 :
4400 REM **** Errors ****
4410 GOSUB 5380:FOR N=1 TO 3:BEEP:NEXT N:LOCATE 2,3
4420 IF BF=0 THEN PRINT"** You pressed the wrong key **"
4430 IF BF=1 OR BF=7 THEN PRINT"** No spaces allowed in name **"
4440 IF BF=2 OR BF=8 THEN PRINT"** No extensions allowed in name **"
4450 IF BF=3 OR BF=9 THEN PRINT"** Name too long **"
4460 IF BF=4 OR BF=10 THEN PRINT"** Numbers not allowed in name **"
4470 IF BF=5 THEN PRINT"** All the numbers on this disk have been used. Start a new disk **"
4480 IF BF=6 THEN PRINT"** '";NM$;"' does not exist or may not be edited **"
4490 LOCATE 4,3:PRINT"Try again"
4500 LOCATE 21,3:PRINT"Press a key to continue. ESCape to return to Main Menu"
4510 CNT$=INKEY$:IF CNT$="" THEN 4510
4520 IF ASC(CNT$)=27 THEN GOTO 110
4530 IF BF=0 THEN 110
4540 IF BF=1 THEN 3070
4550 IF BF=2 THEN 3070
4560 IF BF=3 THEN 3070
4570 IF BF=4 THEN 3070
4580 IF BF=5 THEN 110
4590 IF BF=6 THEN 3320
4600 IF BF=7 THEN 3910
4610 IF BF=8 THEN 3910
4620 IF BF=9 THEN 3910
4630 IF BF=10 THEN 3910
4640 :
4650 REM **** Error Messages ****
4660 IF ERR=58 THEN RESUME NEXT
4670 IF ER=12 OR ER=13 AND ERR=53 THEN RESUME NEXT
4680 GOSUB 5380:FOR N=1 TO 3:BEEP:NEXT N:LOCATE 2,3
4690 IF ERR=53 AND ER=0 THEN PRINT"** There are no files on this disk to load **"
4700 IF ERR=71 THEN PRINT"** Disk not in drive or door open **"
4710 IF ERR=53 AND ER=1 THEN PRINT"** There is no file '";NM$;"' to load from this disk **"
4720 IF ERR=53 AND ER=2 THEN PRINT"** There are no files in memory to save **"
4730 IF ERR=53 AND ER=3 THEN PRINT"** There is no file '";NM$;"' in memory to save **"
4740 IF ERR=53 AND ER=4 THEN PRINT"** There are no files on this disk to delete **"
4750 IF ERR=53 AND ER=5 THEN PRINT"** There is no file '";NM$;"' to delete on this disk **"
4760 IF ERR=70 AND ER=5 THEN PRINT"** Disk write protected **"
4770 IF ERR=53 AND ER=6 THEN PRINT"** There is no message in memory to encipher! **"
4780 IF ERR=53 AND ER=6.1 THEN PRINT"** There is no file '";NM$"' in memory to encipher! **"
4790 IF ERR=53 AND ER=7 THEN PRINT"** Wrong disk. Place data disk in drive **"
4800 IF ERR=53 AND ER=8 THEN PRINT"** Wrong disk. Place data disk in drive **"
4810 IF ERR=70 AND ER=8 THEN PRINT"** Disk write protected. Switch tab to delete numbers **"
4820 IF ERR=53 AND ER=9 THEN PRINT"** There is no file in memory to decipher! **"
4830 IF ERR=5 AND ER=99 THEN PRINT"** You've tried to decipher an un-enciphered message! **":M=5.1
4840 IF ERR=62 AND ER=99 THEN PRINT"** You've tried to decipher an un-enciphered message! **"
4850 IF ERR=53 AND ER=99 THEN PRINT"** There is no file '";NM$"' in memory to decipher! **"
4860 IF ERR=62 AND ER=10 THEN PRINT"** Ending missing. Read in editor or print out **"
4870 IF ERR=9 AND ER=10 THEN PRINT"** Bad data. Can't continue. Print out up to here **"
4880 IF ERR=53 AND ER=10 THEN PRINT"** Wrong disk. Place data disk in drive **"
4890 IF ERR=53 AND ER=10.1 THEN PRINT"** Wrong disk. Place data disk in drive **"
4900 IF ERR=70 AND ER=10.1 THEN PRINT"** Disk write protected. Switch tab to delete numbers **"
4910 IF ERR=53 AND ER=11 THEN PRINT"** Wrong disk. Place data disk in drive **"
4920 IF ERR=58 AND ER=12 THEN PRINT"** You've already used '";NM$"'. Use another name **"
4930 IF ERR=13 AND ER=12 THEN PRINT"** No numbers allowed in name **"
4940 IF ERR=53 AND ER=13 THEN PRINT"** There is no message in memory to edit! **"
4950 IF ERR=53 AND ER=14 THEN PRINT"** Program disk not in drive A **"
4960 IF ERR=71 AND ER=14 THEN PRINT"** Program disk not in drive A **"
4970 IF ERR=24 AND ER=15 THEN PRINT"** Printer off line or not connected **"
4980 IF ERR=53 AND ER=15 THEN PRINT"** There are no messages in memory to print out **"
4990 IF ERR=53 AND ER=16 THEN PRINT"** There is no message '";NM$;"' to print out **"
5000 IF ERR=25 THEN PRINT"** Printer off or not connected **"
5010 IF ERR=27 THEN PRINT"** Printer off or not connected or out of paper **"
5020 IF ERR=53 AND ER=17 THEN PRINT"** There are no messages in memory to save on tape **"
5030 IF ERR=53 AND ER=18 THEN PRINT"** There is no message '";NM$;"' to save on tape **"
5040 IF ERR=57 AND ER=20 THEN PRINT"** Signal corrupted or volume incorrect **"
5050 LOCATE 4,3:PRINT"Try again"
5060 LOCATE 21,3:PRINT"Press a key to continue. ESCape to return to Main Menu"
5070 CNT$=INKEY$:IF CNT$="" THEN 5070
5080 IF ASC(CNT$)=27 THEN CLOSE:GOTO 70
5090 IF ER=0 THEN RESUME 380                           'load 1
5100 IF ER=1 THEN RESUME 380                           'load 2
5110 IF ER=2 THEN RESUME 70                            'save 1
5120 IF ER=3 THEN RESUME 590                           'save 2
5130 IF ER=4 THEN RESUME 70                            'delete 1
5140 IF ER=5 THEN RESUME 820                           'delete 2
5150 IF ER=6 THEN RESUME 70                            'encipher 1
5160 IF ER=6.1 THEN RESUME 1020                         'encipher 2
5170 IF ER=7 THEN RESUME 1120                          'encipher 3
5180 IF ER=8 THEN RESUME 1680                          'encipher 4
5190 IF ER=9 THEN RESUME 70                            'decipher 1
5200 IF ER=99 THEN CLOSE:RESUME 1940                   'decipher 2
5210 IF ER=10 AND ERR=9 THEN CLOSE:RESUME 70           'decipher 3
5220 IF ER=10 AND ERR=62 THEN CLOSE:RESUME 70          'decipher 4
5230 IF ER=10 THEN CLOSE:RESUME 2030                   'decipher 5
5240 IF ER=10.1 THEN RESUME 2850                       'decipher 6
5250 IF ER=11 THEN RESUME 2850                         'decipher 7
5260 IF ER=12 THEN RESUME 3010                         'new
5270 IF ER=13 THEN RESUME 3210                         'old
5280 IF ER=14 THEN RESUME 3330                         'edit
5290 IF ER=15 THEN RESUME 70                           'print 1
5300 IF ER=16 THEN RESUME 3400                         'print 2
5310 IF ER=17 THEN RESUME 70                           'tape 1
5320 IF ER=18 THEN RESUME 3660                         'tape 2
5330 IF ER=20 THEN CLOSE:RESUME 4090                   'tape 3
5340 IF ERR=64 THEN RESUME NEXT
5350 IF ER<0 OR ER>20 THEN CLOSE:RESUME 70
5360 CLOSE:GOTO 70
5370 :
5380 REM **** border ****
5390 CLS:LOCATE 1,1:PRINT CHR$(201)
5400 BDR$=STRING$(78,205):LOCATE 1,2:PRINT BDR$
5410 LOCATE 1,80:PRINT CHR$(187)
5420 FOR V=2 TO 22:LOCATE V,1:PRINT CHR$(186):LOCATE V,80:PRINT CHR$(186):NEXT V
5430 LOCATE 23,1:PRINT CHR$(200)
5440 LOCATE 23,2:PRINT BDR$
5450 LOCATE 23,80:PRINT CHR$(188)
5460 RETURN
5470 :
5480 REM **** Create alphabet ****
5490 OPEN "C:ALFABET" FOR OUTPUT AS#1
5500 FOR N=32 TO 131:PRINT#1,CHR$(N);:NEXT N
5510 CLOSE 1
5520 RETURN
5530 :
5540 OPEN "O",1,"B:SETNUM":PRINT#1,"0":CLOSE
